---
tags: ["Shadre入门精要","图形学"]
categories:
  - 图形学
  - Shadre入门精要
series: ["Shadre入门精要"]
date: 2023-04-11
title: 25.复杂光照 
---
1. 渲染路径(Rendering Path)决定光照如何应用到UnityShader中。为了获取场景中的光源数据，需要为每个Pass指定其渲染路径。
2. Unity支持**前向渲染(Forward Renddering Path)和** **延迟渲染(Deferred Rendering Path)。系统默认情况下选择的是前向渲染路径**，可以更改，同时若想使用多个渲染路径，可以在不同的摄像机中调节 **Rendering Path**选项。
3. 通过在每个Pass中使用标签来指定Pass使用的渲染路径，即使用 **LightMode**标签实现。不同类型的渲染路径可能包含多种标签设置。

```text
Pass{
	Tags{"LightMode"="ForwardBase"}
}
```

前向渲染路径除了有**ForwardBase**，还有 **ForwardAdd**。Pass中的 **LightMode**支持的渲染路径设置选项：

![img](/images/posts/v2-a642f38a4d613af58757bb370273ba60_720w.png)

当我们==为一个Pass设置了渲染路径的标签==，就可以通过Unity提供的内置光照变量来访问这些属性。

## **前向渲染路径**

每进行一次完整的前向渲染，都需要渲染该对象的渲染图元，并计算颜色和深度缓冲区的值。**对于每一个逐像素光源，都需要进行一次该过程。**也就是说，如果一个物体==在多个逐像素光源的影响区域内，那么就需要执行对应数量的Pass==，每个Pass对应一个逐像素的光源，然后在帧缓冲内将这些 **光照结果**混合起来得到最终的颜色值。

**Unity中的前向渲染**
Unity中，前向渲染路径有3种处理光照的方式：**逐顶点处理，逐像素处理，球谐函数** 光源类型和渲染重要度决定了使用哪一种处理方式。
光源类型指该光源是平行光还是其他类型的光源。
渲染重要度是指该光源是否为**重要的(IMPORTANT)**，如果将一个光源设置为重要的，那么Unity将其作为逐像素光源来处理。
在前向渲染中，Unity根据场景中的光源对物体的影响程度对光源进行重要度排序。
一定数目光源按照 **逐像素**处理
最多 **4个**光源按照 **逐顶点**方式处理
剩下的光源按照 **球谐函数**方式处理
Unity使用的判定规则：

- 最亮的平行光总是按照逐像素处理
- 渲染模式设置为 **Not Important**的光源，按照逐顶点或球谐函数的方式处理
- 渲染模式被设置成**Important**的光源，按逐像素处理
- 根据以上规则得到的逐像素光源数量小于**QualitySetting**中的逐像素光源数量，将有更多的光源以逐像素方式进行渲染

光照计算在Pass中，前向渲染有两种：**ForwardBase**和 **ForwardAdd**，使用如下：

![img](/images/posts/v2-b070f28db0130e23ef191712eab98166_720w.png)



- ==Base Pass中渲染的平行光默认支持阴影，Additional Pass中渲染的光源默认情况没有阴影效果，需要使用 #pragma multi-compile-fwdadd-fullshadows代替 #pragma multi-compile-fwdadd编译指令==。
- ==环境光和自发光只需计算一次，因此放到BasePass中==
- Additional Pass的渲染设置中，==开启和设置设置混合模式==，因为希望每个Additional Pass可以与上一次的光照结果在帧缓存中进行叠加。如果没有设置，那么就会将上一次的计算结果进行覆盖。通常设置的混合模式为**Blend One One**
- 针对前向渲染，一个Shader通常会定义一个Base Pass（Base Pass也可以定义多个，像之前的双面渲染）和一个Additional Pass。一个Base Pass仅执行一次，而Additional Pass会根据影响该物体的其他逐像素光源的数目被多次调用。

**内置的光照变量&函数**
前向渲染中可以使用的内置光照变量

![img](/images/posts/v2-03242399a10c61ae04a1430827caaad0_720w.png)

前向渲染中可以使用的内置光照函数

![img](/images/posts/v2-7446797102c54537d864788c24b99335_720w.webp)

## **Unity光源类型**

Unity支持4种光源类型：平行光、点光源、聚光灯和面光源。面光源只有在烘焙后才能起作用，不属于实时光源。Shader中常用的光源属性包括：光源的位置、方向、颜色、强度和衰减。

- **平行光**
  平行光对照亮的范围没有限制，平行光的几何属性只有方向。==平行光没有衰减的概念==。
- **点光源**
  点光源的照亮空间是有限的，由空间中的一个球体定义。点光源可以表示由一个点发出、向所有方向延伸的光。点光源具有位置、方向、颜色、强度和衰减的属性。
- **聚光灯**
  聚光灯的照亮空间是有限的，由空间中的一个锥形区域定义。聚光灯同样具有位置、方向、颜色、强度和衰减的属性。聚光灯的衰减比点光源的衰减计算更为复杂，因为要判断是否在一个锥形区域内。

## **前向渲染中处理不同的光源类型**

使用一个平行光和一个点光源共同照亮物体

1. 第一个pass **ForwardBase**

   ```cs
   fixed4 frag(v2f i) : SV_Target
   {
       // sample the texture
   
       // 环境光和自发光只需计算一次，因此放到BasePass中
       //ambient环境光
       fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz;
   
       float3 normalDir = normalize(i.worldNormal);
       float3 lightDir = normalize(UnityWorldSpaceLightDir(i.worldPos));
       float3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
       float3 halfDir = normalize(viewDir + lightDir);
   
   
       fixed3 specular = _LightColor0.rgb * pow(dot(halfDir, normalDir), _Gloss);
       fixed3 diffuse = _LightColor0.rgb * max(dot(normalDir, lightDir), 0);
   
       //设置衰减为一 无衰减
       float atten = 1.0;
       return fixed4(ambient + (specular + diffuse) * atten, 1);
   }
   ```

   

2. 第二个pass **ForwardAdd**

   ==unity_WorldToLight 和_LightTexture0 报错不要管==

   记得一定引入

   ` #include "Lighting.cginc"  ` 

   `#include "AutoLight.cginc"`

   ==要点在于== 判断处理的光源是否为平行光，如果是平行光，渲染引擎会定义USING_DIRECTIONAL_LIGHT

   ` fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);`

   若没有定义则说明不是平行光，光源位置通过运算得到

   `fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz - i.worldPos.xyz);`

   ==同样处理衰减时==  ==这里看不明白下一节会讲==

   为平行光
   `fixed  atten=1.0;`
   针对其他光源类型，Unity使用一张纹理作为查找表，来得到片元着色器中的光照衰减值
   `   float3 lightCoord = mul(unity_WorldToLight, float4(i.worldPos, 1)).xyz;`   

    `//Unity使用的纹理查找表_LightTexture0 `   

   `//先得到光源空间下的坐标，使用该坐标进行采样得到衰减值
           fixed atten = tex2D(_LightTexture0, dot(lightCoord, lightCoord).rr).UNITY_ATTEN_CHANNEL;`

   ```cs
   Pass
   {
       //针对前向渲染，一个Shader通常会定义一个Base Pass（Base Pass也可以定义多个，像之前的双面渲染）和一个Additional Pass。一个Base Pass仅执行一次，而Additional Pass会根据影响该物体的其他逐像素光源的数目被多次调用。
       // Pass for other pixel lights
       Tags
       {
           "LightMode"="ForwardAdd"
       }
   
       Blend One One
       //Additional Pass的渲染设置中，开启和设置设置混合模式，因为希望每个Additional Pass可以与上一次的光照结果在帧缓存中进行叠加。如果没有设置，那么就会将上一次的计算结果进行覆盖。通常设置的混合模式为Blend One One
   
       CGPROGRAM
       // Apparently need to add this declaration
       #pragma multi_compile_fwdadd
   
       #pragma vertex vert
       #pragma fragment frag
   
       #include "Lighting.cginc"
       #include "AutoLight.cginc"
   
       fixed4 _Diffuse;
       fixed4 _Specular;
       float _Gloss;
   
       struct a2v
       {
           float4 vertex : POSITION;
           float3 normal : NORMAL;
       };
   
       struct v2f
       {
           float4 pos : SV_POSITION;
           float3 worldNormal : TEXCOORD0;
           float3 worldPos : TEXCOORD1;
       };
   
       v2f vert(a2v v)
       {
           v2f o;
           o.pos = UnityObjectToClipPos(v.vertex);
   
           o.worldNormal = UnityObjectToWorldNormal(v.normal);
   
           o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
   
           return o;
       }
   
       fixed4 frag(v2f i) : SV_Target
       {
           //上述过程先判断处理的光源是否为平行光，如果是平行光，渲染引擎会定义USING_DIRECTIONAL_LIGHT
           //若没有定义则说明不是平行光，光源位置通过运算得到
           #ifdef USING_DIRECTIONAL_LIGHT
               fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);
           #else
           fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz - i.worldPos.xyz);
           #endif
   
   
           fixed3 worldNormal = normalize(i.worldNormal);
           fixed3 worldViewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
   
           fixed3 diffuse = _LightColor0.rgb * max(0, dot(worldNormal, worldLightDir));
           fixed3 halfDir = normalize(worldViewDir + worldLightDir);
           fixed3 specular = _LightColor0.rgb * pow(
               saturate(dot(worldNormal, halfDir)), _Gloss);
   
           //处理衰减过程
           #ifdef USING_DIRECTIONAL_LIGHT//为平行光
               fixed  atten=1.0;
           #else//为点其他光源
            //针对其他光源类型，Unity使用一张纹理作为查找表，来得到片元着色器中的光照衰减值
           float3 lightCoord = mul(unity_WorldToLight, float4(i.worldPos, 1)).xyz; //先得到光源空间下的坐标，使用该坐标进行采样得到衰减值
           fixed atten = tex2D(_LightTexture0, dot(lightCoord, lightCoord).rr).UNITY_ATTEN_CHANNEL;
           #endif
   
   
           return fixed4((diffuse + specular) * atten, 1.0);
       }
   ```

   ![image-20230430170030338](/images/posts/image-20230430170030338.png)
